#!/usr/bin/env raku
#| Given a map with trees indicated by #, returns the number of trees that would be hit
#| with a slope of 3/1 (p1) or the product of the numbes of trees that would be hit (p2)
sub MAIN(Bool :$p2=?0, Bool :$p1=!$p2) {
    my @area = lines.map({|.comb xx ∞});
    
    $p1 ?? 3/1 !! (1/1, 3/1, 5/1, 7/1, 1/2)
    ==> map( -> $slope {
        my ($row, $col) = (0, 0);
        gather { while $col < @area.elems {
            take @area[$col; $row] eq '#';
            ($row, $col) «+=» $slope.Rat.nude;
        }}.sum })
    ==> reduce(* × *)
    ==> {.say; $_}()
}

# Tests (run with `raku --doc -c $FILE`)
DOC CHECK { use Test;
    $*IN = q:to/EOF/;
        ..##.......
        #...#...#..
        .#....#..#.
        ..#.#...#.#
        .#...##..#.
        ..#.##.....
        .#.#.#....#
        .#........#
        #.##...#...
        #...##....#
        .#..#...#.#
        EOF
    MAIN(:p1).&is(7);
    MAIN(:p2).&is(336);
}
